---
title: "4Clojure for learning Clojure"
categories: clojure
---

## I completed all of the 4clojure problems!

After four weeks of consistent focus I made it through all 156 problems on
[4clojure.com][4clojure] Proof: [my user page][devm33] and [list of top
users][users].

You can also see most of my solutions in the [github repo][github] I used for
this project. I only committed the problems to the repo that I needed to develop
inside the editor/repl, whereas some problems were simple enough to be solved on
the website. So the ones in the repo should be the more interesting problems.

I'd like to share here some of my favorite problems and solutions. Warning:
these are spoilers for 4clojure of course. And if you're interested in learning
clojure I'd highly recommend not reading this and attempting the problems
yourself. The problems range from teaching you the basics of the language all
the way to really rather challenging. Each problem is presented with a set of
unit tests. Then once you solve a problem you can see everyone else's solutions,
which is great for benchmarking yourself and picking up other people's tricks.

This was my first adventure into clojure and I found the 4clojure problems great
for motivating me through the initial learning curve and maybe a bit past it. Of
course after 4 weeks I'm definitely still a beginner with clojure so any
feedback is welcomed!

## Fibonacci

<http://www.4clojure.com/problem/26>

The classic Fibonacci series problem. A goto for learning any language
especially a functional language. This was the first problem I had to write down
because I kept tripping myself with how `conj` adds to the begining of lists. This
frustration is definitely appreciatable in my submitted solution.


{% highlight clojure %}

(defn fib [n]
  (if (< n 3)
    (repeat n 1)
    (let [prev (reverse (fib (dec n)))]
      (reverse (conj prev (+ (first prev) (second prev)))))))

{% endhighlight %}

This approaches works but is pretty kludgy and far from efficient. To take
another crack at this knowing what I know now I'd opt for using a lazy sequence.

{% highlight clojure %}

(defn fibo [n]
  (letfn [(fib [a b] (cons a (lazy-seq (fib b (+ a b)))))]
    (take n (fib 1 1))))

{% endhighlight %}

Another good approach would be using `loop` and `recur`.


{% highlight clojure %}

(defn fibloop [n]
  (loop [remain n ret [] a 0 b 1]
    (if (= 0 remain) ret
      (recur (dec remain)
             (conj ret a)
             b
             (+ a b)))))

{% endhighlight %}

## Replicate a sequence

<http://www.4clojure.com/problem/33>

While I came up with the alternative fibonacci solutions for this post many of
the alternatives I saved alongside my solutions were taken from the other
user's solutions. For example for this problem repeating each element in a
series I was content with my solution

{% highlight clojure %}

(defn repf [col num]
  (reduce #(concat %1 (repeat num %2)) '() col))

{% endhighlight %}

While there's nothing wrong with this solution persay -- it's nearly equivalent
techinically to the better one below what I learned from this other user (\_pcl)
was that there was already a core api call for part of what I was doing
[mapcat][mapcat].

{% highlight clojure %}

(defn better_repf [col num]
  (mapcat (partial repeat num) col))

{% endhighlight %}

Insights like this from the feedback of other users' solutions were tremendously
useful in learning different parts of the clojure api's very quickly.

## Sequence Reductions

<http://www.4clojure.com/problem/60>

Another place I found that I got great feedback was the clojure source code.
This was introduced to me since some of the 4clojure problems consist of
reimplementing core functions. The clojure doc's link to the relevant, which I
greatly appreciate.

<https://clojuredocs.org/clojure.core/reductions> See the link to source in the
top right under clojure.core.

<https://github.com/clojure/clojure/blob/clojure-1.6.0/src/clj/clojure/core.clj#L6628>

{% highlight clojure %}

(defn reductions
  "Returns a lazy seq of the intermediate values of the reduction (as
  per reduce) of coll by f, starting with init."
  {:added "1.2"}
  ([f coll]
     (lazy-seq
      (if-let [s (seq coll)]
        (reductions f (first s) (rest s))
        (list (f)))))
  ([f init coll]
     (cons init
           (lazy-seq
            (when-let [s (seq coll)]
              (reductions f (f init (first s)) (rest s)))))))

{% endhighlight %}

I found the clojure source very readable and approachable. Once I started
reading the source for the core-function problems I started referencing it for
other problems as well.


## Prime Numbers

<http://www.4clojure.com/problem/67>

I didn't learn anything in particular from this problem I just love this method
of testing primality.

{% highlight clojure %}

; first n prime numbers
; sneaky regex version ;]

(defn __ [n]
  (take
    n
    (filter
      #(empty? (re-matches #"^1?$|^(11+?)\1+$" (apply str (repeat % "1"))))
      (range))))

{% endhighlight %}

Naming the solution function `__` double underscores was a trick I picked up to
make copying and pasting the testcases from 4clojure quicker, e.g.

{% highlight clojure %}

(assert (= (__ 2) [2 3]))

(assert (= (__ 5) [2 3 5 7 11]))

(assert (= (last (__ 100)) 541))

{% endhighlight %}

With a vim mapping for adding the assert's of course. The vim set up for clojure
should probably be a post to itself because that took a minute.

## Language of a DFA

<http://www.4clojure.com/problem/164>

The problems on 4clojure are divided into four tiers of difficulty: elementary,
easy, medium, and hard. Elementary and easy were often the kind that didn't
require use of the editor or repl or anything and could be solved on the website
often in one line or one word. The medium and many of the hard were great for
making me think a little deeper about the language and use it a more throughly
for a substantive problem. The last ten or so of the hard category, however,
were a different kind of beast entirely. While I was able to solve the first 140
problems in the first three weeks of this project the last dozen or so that I
had left took up a disportionate amount of time. I really liked this. They were
legitimately difficult.

One of my favorite from this group was a problem asking to produce the language
of [deterministic finite automaton (DFA)][DFA]. The class that introduced me to
formal languages and their machines was one of my favorite in college, so I'm
always excited to see something like a DFA come up. I also like this problem
because it highlights some of the utility of lazy-execution that is easily
available in clojure.

{% highlight clojure %}

(defn __ [dfa]
  (letfn
    [(map-trans [cur trans]
       (when trans
         (map (fn [[t s]] {:state s :path (str (:path cur) t)}) trans)))
     (enq [trails]
       (when (not-empty trails)
         (lazy-seq
           (let [cur (first trails)
                 next-trans (get-in dfa [:transitions (:state cur)])
                 next-trails (concat (rest trails) (map-trans cur next-trans))]
             (if ((:accepts dfa) (:state cur))
               (cons (:path cur) (enq next-trails))
               (enq next-trails))))))]
    (enq [{:state (:start dfa) :path ""}])))

(assert (= #{"a" "ab" "abc"}
           (set (__ '{:states #{q0 q1 q2 q3}
                      :alphabet #{a b c}
                      :start q0
                      :accepts #{q1 q2 q3}
                      :transitions {q0 {a q1}
                                    q1 {b q2}
                                    q2 {c q3}}}))))

{% endhighlight %}

At each step in the recursion I have passed down a sequence of substring objects
(hashmaps) `trails`, which is used as a queue. For the first substring out of
this queue, I enqueue all the states to which the DFA could transition from that
substring's state. Then before recurring I check if the current substring is
accepted by the DFA (simply by checking its' state) and if so I add it to the
lazy-seq. Either way I recurse with the new queue of substrings and states
`next-trails`.

While the problem wasn't the hardest of the hard (see [140][140] [152][152] or 
[127][127]) it's a great example of where my clojure writing is now at the end
of these four weeks. There's several aspects of the language that I'm really
enjoying that are used here:

- hashmap objects
{% highlight clojure %}
{:state (:start dfa) :path ""}
{% endhighlight %}
- keywords as getter functions from hashmap objects
{% highlight clojure %}
(:start dfa)
{% endhighlight %}
- argument destructuring
{% highlight clojure %}
(map
  (fn [[t s]]
    ; this function is passed a key-value pair for each character
    ; state pair in the iterated map of transitions
    {:state s :path (str (:path cur) t)})
  trans)
{% endhighlight %}
- the let macro's first-to-last evaluation
{% highlight clojure %}
(let [cur (first trails)
      next-trans (get-in dfa [:transitions (:state cur)])
{% endhighlight %}
- and as I already mentioned availablity of lazy evaluation functions

In conclusion, I had a lot of fun going through 4clojure and am very thankful to
the creators and maintainers. I look forward to more with clojure in the future.


[4clojure]: http://www.4clojure.com/
[devm33]: http://www.4clojure.com/user/devm33
[users]: http://www.4clojure.com/users
[github]: https://github.com/devm33/4clojure_problems
[mapcat]: https://clojuredocs.org/clojure.core/mapcat
[DFA]: http://en.wikipedia.org/wiki/Deterministic_finite_automaton
[140]: http://www.4clojure.com/problem/140
[152]: http://www.4clojure.com/problem/152
[127]: http://www.4clojure.com/problem/127
